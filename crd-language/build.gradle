buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/ciprian-teodorov/CipT-artefacts/"
        }
    }
    dependencies {
        classpath group: 'fr.ensta', name: 'ecore2java', version: '1.5'
    }
}
apply plugin: 'fr.ensta.ecore2java'

configurations {
    antlr4
    ecore
    compile.extendsFrom antlr4
    compile.extendsFrom ecore
}
dependencies {
	antlr4  group: 'org.antlr', name: 'antlr4' , version: '4.5.+'

    compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.common',               version: '2.11.0-v20150123-0347'
    compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore',                version: '2.11.0-v20150123-0347'
    compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi',            version: '2.11.0-v20150123-0347'
}


String antlr4OutputPath(String path) {
    if (System.properties['os.name'].replaceAll(' ', '').toLowerCase().contains('win')) {
        return file(path + '/grammar')
    }
    return file(path)
}
def generatedPath = file('generated')
generatedPath.mkdir()
task generateAntlr4Parser(type: JavaExec) {
    classpath configurations.antlr4
    main = "org.antlr.v4.Tool"
    args = ["grammar/ClockRDL.g4"
        , "-package"
        , "ClockRDL.grammar"
        , "-o"
        , antlr4OutputPath("generated/ClockRDL")]
}

ecore2java {
    genModelPath = file('model/ClockRDL.genmodel')
    outputDir = generatedPath
}

sourceSets.main.java.srcDir 'generated'
compileJava.dependsOn generateAntlr4Parser, ecore2java

clean { doLast { project.delete "tmp" } }

test {
    outputs.upToDateWhen { false }
}
